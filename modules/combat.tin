#IF {$is_startup}
{
    #VAR phelp_list[combat][setrush] {Sets the rush trigger response.};
    #VAR phelp_list[combat][jtform] {Automatically interacts with the tform mob in Japan.};
    #VAR phelp_list[combat][ctform] {Automatically interacts with the tform mob in Crusades.};
    #VAR phelp_list[combat][ttform] {Automatically interacts with the tform mob in Tara.};
};

#ACTION {{^(?i)(?:(?:a|an|small|the|one|of)\s)*([A-Za-z'\-]+)\,?.*?rushes at(?!\syou\s)(?:\s([\w'\.\-]+))*(?:,\s.*?)? and attacks!$}}
{
    #IF {"%2" == "Somebody"}
    {
        #VAR somebody_check %3;
        #ACTION {{^(?i)(?:(?:a|an|small|the|one|of)\s)*([A-Za-z'\-]+)\,?.*?fades into existence.$}}
        {
            rush %%2 $somebody_check;
            #UNVAR somebody_check;
            #UNACTION {{^(?i)(?:(?:a|an|small|the|one|of)\s)*([A-Za-z'\-]+)\,?.*?fades into existence.$}}
        }
    }
    {
        rush %2 %3;
    }
}
{4}

#ALIAS {setrush %*}
{
    #SHOWME Alias "rush" set to %1.;
    #ALIAS {rush %%1 %%2} {%1};
}

#ALIAS {rush %1 %2}
{
    #SHOWME <019>Rush trigger not yet configured, use the "setrush" alias to configure.<099>;
}

#ACTION {PANIC! You couldn't escape!}
{
    #IF {"$auto_flee" == "true"}
    {
        #SEND flee;
    }
}
{5}

#ACTION {{^You feel quite a bit better.$}}
{
	#SEND report;
}
{5}

#ACTION {{^You get (\d+) gold coins from the .*?corpse of (.*?).$}}
{
	#IF {"%3" != "$prompt[name]"}
	{
		#SEND split %2;
	}
}
{5}

#ACTION {{^You hear (.*?)'s death cry.$}}
{
	#BUFFER get xp_check -5 -1;
    #LOOP 5 1 count
    {
        #LOCAL line {$xp_check[$count]};
        #REGEX {$line} {{You receive \d+ experience.}}
        {
            #SEND get coin corpse,money corpse;
            #SEND stand;
        };
    };
    #UNVAR xp_check;
    #UNVAR count;
}
{5}

#ACTION {{and all of a sudden, you find yourself sitting on the floor.$}}
{
	#SEND stand;
}
{5}

#ACTION {{puts an augmented cloth poultice on your wounds.$}}
{
	#SEND report;
}
{5}

#ACTION {{digs into your flesh!$}}
{
    #SEND report;
}
{5}

#ALIAS {jtform %1}
{
    #REGEX {%1} {{(?:\d+\.)?(.*)}}
    {
        #VAR tform_kw &2;
        #SEND remove %1;
        #SEND give $tform_kw old;
        #ACTION {{^A mysterious shugenja gives .*? to you.$}}
        {
            #SEND wear $tform_kw;
            #UNVAR tform_kw;
            #UNACTION {{^A mysterious shugenja gives .*? to you.$}};
        }
    };
}

#ALIAS {ctform %1}
{
    #REGEX {%1} {{(?:\d+\.)?(.*)}}
    {
        #VAR tform_kw &2;
        #SEND give 100 coins sufi;
        #SEND remove %1;
        #SEND give $tform_kw sufi;
        #ACTION {{^An aging Sufi gives you.*$}}
        {
            #SEND wear $tform_kw;
            #UNVAR tform_kw;
            #UNACTION {{^An aging Sufi gives you.*$}};
        }
    }
}

#ALIAS {ttform %1}
{
    #REGEX {%1} {{(?:\d+\.)?(.*)}}
    {
        #VAR tform_kw &2;
        #SEND enq ask sadhbh TRUEFORM;
        #SEND remove %1;
        #SEND give $tform_kw sadhbh;
        #ACTION {{^Sadhbh gives you.*$}}
        {
            #SEND wear $tform_kw;
            #UNVAR tform_kw;
            #UNACTION {{^An aging Sufi gives you.*$}};
        }
    }
}
